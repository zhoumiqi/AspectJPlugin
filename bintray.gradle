apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def projectName = "AspectJPlugin"
def mavenDesc = 'gradle plugin aspectj for aop'
def baseUrl = 'https://github.com/zhoumiqi'
def siteUrl = baseUrl
def gitUrl = "${baseUrl}/AspectJPlugin.git"
def issueUrl = "${baseUrl}/AspectJPlugin/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2017'

def username = 'zhoumiqi'
//版本号,必填
version = "1.0.0"
//groupId,必填(一般填唯一的包名)
group = "com.zmq.plugin"

//.参考：http://blog.csdn.net/sbsujjbcy/article/details/50782830/
// 更新版本执行 gradlew install , gradlew bintrayUpload


install {
    repositories {
        mavenInstaller {
            // 生成 POM.xml with proper parameters
            pom.project {
                //项目名称Description
                name projectName
                //项目描述
                description mavenDesc
                url siteUrl

                // Archive
//                groupId project.group
                artifactId archivesBaseName
//                version project.version

                // License软件开源协议，现在一般都是Apache License2.0
                inceptionYear inception
                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                //开发者基本信息
                developers {
                    developer {
                        name username
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}


task javadocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//配置bintray参数
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = 'architecture_zmq'
        name = projectName
        desc = mavenDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['gradle', 'plugin', 'aspectj']
        licenses = licenseIds
        publish = true
        publicDownloadNumbers = true
    }
}